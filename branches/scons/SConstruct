# SCons script for librascal.
# $Id$
# vim:ts=2:sts=2:sw=2:noet:nocindent:syntax=python

import os
import re

class Finder:
  # A function to list files in a directory.
  def listFilesW(self, at, r):
    nodes = []
    for location in at:
      for file in os.listdir(location):
        fname = os.path.join(location, file)
        if os.path.isfile(fname):
          if r.match(file):
            nodes.append(fname)
        elif os.path.isdir(fname):
          if not fname == '.svn':
            for more in self.listFilesW([fname], r):
              nodes.append(more)
    return nodes
  
  def listFiles(self, at, r):
    l = self.listFilesW(at, re.compile(r))
    l.sort()
    return l

# Version number.
version = '1.0'

Help("""
Usage: scons [options]

The following options are available:

  debug=yes   : include debug symbols and output.
  release=yes : set when you are preparing a release package.
  samples=yes : build sample programs, too.

Send your comments to Justin Forest <justin.forest@gmail.com>
""")

# The directory in which built files are stored.
output_dir = 'build'

fnd = Finder()

# Build options.
opts = Options('config.options')
opts.Add(BoolOption('debug', 'Set to include debug symbols and output', False))
opts.Add(BoolOption('samples', 'Set to build sample programs', False))
opts.Add(BoolOption('release', 'Set this option only if you are preparing a release', False))
opts.Add(EnumOption('build_tool', 'Override the build tool (for maintainers mostly)', 'default', allowed_values = ('default', 'mingw'), ignorecase = 1))

# The default environment.
env = Environment(options = opts)

# Override the tool if there are many installed.
if env['build_tool'] != 'default':
	env = Environment(tools = [env['build_tool']], options = opts)

env.Append(CPPDEFINES = [('VERSION', version)])

# Check the system configuration if we're not cleaning up.
if env.GetOption('clean'):
	print 'Skipping autoconfiguration due to a clean-up.'
else:
	print 'Configuring for platform: ' + env['PLATFORM'] + '.'
	conf = Configure(env)
	if not conf.CheckFunc('strlcpy'):
		print 'Could not find strlcpy(), using local version.'
		conf.env.Append(CPPDEFINES = 'strlcpy=strlcpy_local')
	if conf.CheckFunc('getopt'):
		conf.env.Append(CPPDEFINES = 'HAVE_getopt')
	if env['PLATFORM'] == 'win32':
		conf.env.Append(CPPDEFINES = 'WIN32_LEAN_AND_MEAN')
		# Windows specific.
		if not conf.CheckLib('ws2_32'):
			print 'The winsock2 library is not available (very unusual).'
			Exit(1)
		if not conf.CheckLib('mswsock'):
			print 'The mswsock library is required (usually available on nt).'
			Exit(1)
		if not conf.CheckLib('iphlpapi'):
			print 'The iphlpapi library is required (usually available on nt).'
			Exit(1)
		conf.CheckLib('user32')
		if not conf.CheckLib(['pthread', 'pthreadGC', 'pthreadVC']):
			print 'You must have the pthread library installed.'
			Exit(1)
		if not conf.CheckFunc('snprintf'):
			if conf.CheckFunc('_snprintf'):
				conf.env.Append(CPPDEFINES = 'snprintf=_snprintf')
			else:
				print 'The snprintf() function is not avaialble (very unusual).'
				Exit(1)
		if not conf.CheckFunc('vsnprintf'):
			if conf.CheckFunc('_vsnprintf'):
				conf.env.Append(CPPDEFINES = 'vsnprintf=_vsnprintf')
			else:
				print 'The vsnprintf() function is not avaialble (very unusual).'
				Exit(1)
	else:
		# POSIX specific.
		if not conf.CheckLib('pthread'):
			print 'You must have the pthread library installed.'
			Exit(1)
	if env['debug'] == 1:
		if conf.CheckLib('flog'):
			conf.env.Append(CPPDEFINES = 'HAVE_flog')
	if not conf.CheckCHeader('pthread.h'):
		print 'You must have the pthread library properly installed.'
		Exit(1)
	env = conf.Finish()

# Platform-dependent code.
if env['PLATFORM'] == 'win32':
	env['LIBARCH'] = 'nt'
else:
	env['LIBARCH'] = 'bsd'

# Adapt to a debug build.
if env['debug'] == 1:
	print '*** DEBUG BUILD ***'
	env.Append(CPPDEFINES = '_DEBUG')
	if env['CC'] == 'cl':
		env.Append(CCFLAGS = '/Od /GX /EHsc /MDd /Z7')
		env.Append(LINKFLAGS = '/DEBUG /OPT:NOICF /OPT:NOREF')
	elif env['CC'] == 'gcc':
		env.Append(CCFLAGS = '-g -O0')

# Adapt to a release build.
else:
	if env['CC'] == 'cl':
		env.Append(CCFLAGS = '/Ox /G5 /GF /Gy /MD')
		env.Append(LINKFLAGS = '/RELEASE /OPT:REF /OPT:ICF')
	elif env['CC'] == 'gcc':
		env.Append(CCFLAGS = '-O3 -fomit-frame-pointer -finline-functions')
		env.Append(LINKFLAGS = '-s --gc-sections')

opts.Save('config.options', env)
Export('env', 'fnd')

# Build the library.
SConscript(os.path.join('sources', env['LIBARCH'], 'SConscript'))

# Build the samples.
if env['samples'] == '1':
	SConscript('sources/samples/SConscript')

if env['PLATFORM'] != 'win32':
	# Build a source tarball.
	Environment(TARFLAGS = '-c -z').Tar('librascal-' + version + '-src.tar.gz', fnd.listFiles(['.'], r'^(LICENSE|SConstruct|.*\.(cc|h|rc|docbook|dtd|css))$'))
